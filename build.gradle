plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '1.0.21'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}


repositories {
    mavenLocal()
    maven {
        url "https://maven.blamejared.com/"
    }
    maven {
        url "https://maven.architectury.dev/"
    }
    maven {
        url "https://maven.terraformersmc.com/"
    }
}

dependencies {
    compileOnly "me.shedaniel:RoughlyEnoughItems-neoforge:16.0.799"

    compileOnly("mezz.jei:jei-1.21.1-neoforge-api:19.21.1.300")

    compileOnly "dev.emi:emi-neoforge:1.1.22+1.21.1:api"
    runtimeOnly "dev.emi:emi-neoforge:1.1.22+1.21.1"

    jarJar(implementation "org.commonmark:commonmark") {
        version {
            strictly '[0.1, 1.0)'
            prefer '0.24.0'
        }
    }
    additionalRuntimeClasspath "org.commonmark:commonmark:0.24.0"
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Jar).configureEach {
    archiveClassifier = "neoforge-"+minecraft_version
    manifest {
        attributes["Main-Class"] = "dev.lucaargolo.charta.editor.CardEditor"
    }
    exclude("cards/**")
    exclude("decks/**")
    exclude("suits/**")
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
